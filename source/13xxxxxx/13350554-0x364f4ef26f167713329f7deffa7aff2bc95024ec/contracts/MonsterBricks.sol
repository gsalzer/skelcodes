/*

 .----------------. .----------------. .-----------------..----------------. .----------------. .----------------. .----------------.
| .--------------. | .--------------. | .--------------. | .--------------. | .--------------. | .--------------. | .--------------. |
| | ____    ____ | | |     ____     | | | ____  _____  | | |    _______   | | |  _________   | | |  _________   | | |  _______     | |
| ||_   \  /   _|| | |   .'    `.   | | ||_   \|_   _| | | |   /  ___  |  | | | |  _   _  |  | | | |_   ___  |  | | | |_   __ \    | |
| |  |   \/   |  | | |  /  .--.  \  | | |  |   \ | |   | | |  |  (__ \_|  | | | |_/ | | \_|  | | |   | |_  \_|  | | |   | |__) |   | |
| |  | |\  /| |  | | |  | |    | |  | | |  | |\ \| |   | | |   '.___`-.   | | |     | |      | | |   |  _|  _   | | |   |  __ /    | |
| | _| |_\/_| |_ | | |  \  `--'  /  | | | _| |_\   |_  | | |  |`\____) |  | | |    _| |_     | | |  _| |___/ |  | | |  _| |  \ \_  | |
| ||_____||_____|| | |   `.____.'   | | ||_____|\____| | | |  |_______.'  | | |   |_____|    | | | |_________|  | | | |____| |___| | |
| |              | | |              | | |              | | |              | | |              | | |              | | |              | |
| '--------------' | '--------------' | '--------------' | '--------------' | '--------------' | '--------------' | '--------------' |
 '----------------' '----------------' '----------------' '----------------' '----------------' '----------------' '----------------'

          .----------------. .----------------. .----------------. .----------------. .----------------. .----------------.
         | .--------------. | .--------------. | .--------------. | .--------------. | .--------------. | .--------------. |
         | |   ______     | | |  _______     | | |     _____    | | |     ______   | | |  ___  ____   | | |    _______   | |
         | |  |_   _ \    | | | |_   __ \    | | |    |_   _|   | | |   .' ___  |  | | | |_  ||_  _|  | | |   /  ___  |  | |
         | |    | |_) |   | | |   | |__) |   | | |      | |     | | |  / .'   \_|  | | |   | |_/ /    | | |  |  (__ \_|  | |
         | |    |  __'.   | | |   |  __ /    | | |      | |     | | |  | |         | | |   |  __'.    | | |   '.___`-.   | |
         | |   _| |__) |  | | |  _| |  \ \_  | | |     _| |_    | | |  \ `.___.'\  | | |  _| |  \ \_  | | |  |`\____) |  | |
         | |  |_______/   | | | |____| |___| | | |    |_____|   | | |   `._____.'  | | | |____||____| | | |  |_______.'  | |
         | |              | | |              | | |              | | |              | | |              | | |              | |
         | '--------------' | '--------------' | '--------------' | '--------------' | '--------------' | '--------------' |
          '----------------' '----------------' '----------------' '----------------' '----------------' '----------------'


                          MMMMMOOOOOOOOOOOOOOZZZZZZZOOOOOOZZZOZZZZOOOOOOOOOOOOOOOOOOO8DDDDDDNMOD8OOOMMMMMM
                          MMMMM$$Z$$ZZZ$$$$$$$$$ZZZZZZZ$Z$$$Z$$$Z$Z$7$$ZZZ$ZZZ$ZZZ$$7ZZ$ZZZZZZZZZ$ZZMMMMMM
                          MMMMMZZZ$Z$$Z$$$OZ?????ZO$ZZ$$$Z$ZZ$$$ZZ$Z$$$ZZ$$$$$Z$$$ZOO+I??+ZOZZ$$ZZZZMMMMMM
                          MMMMMZ$$Z$Z$$O??OO+,,:DO?+7O$$Z$$$ZZ$$$Z$$Z$Z$$$7$7$Z$O??+?ZZDZO???OZZZZZZMMMMMM
                          MMMMM$Z$$$$O?I:,,~OOO=,,:O?+O$ZZZ$$$$$$$$$ZZZ$$$Z$$ZO??I?,,=OI,::8++O$$$Z$MMMMMM
                          MMMMM$$$7Z$??,,8$$$$$$$$8,O++OZ$$$7$$ZZ$$$$$$$$$Z$$$??O:O$$$$$$Z:,7??Z$$$ZMMMMMM
                          MMMMMZ$$$$O?Z,Z$$$Z$7$7$$O,D??$$$Z7$7ZZZZ7Z$Z$$$Z$$Z+Z,$$$$$$$7$$O,O?OZ$$$MMMMMM
                          MMMMMZ$ZZZO?,.$$$$$7$$7$7$:,O?O$Z$$7Z$$OOO$$$$$Z$$7+O,O7$$7$$$$$$Z:8?$$$$$NMMMMM
                          MMMMMO$7$ZO?,,$$$$$$7$$$$$+,O?O$$$Z$7OZOOZOO$$$ZZZZ?7,8$$7$77$$$$$:D??$ZZ$DMMMMM
                          MMMMMOZ7$Z$?D,$$7OOIOZ$7$$,,?+Z$$$$$OOOZZZOOOZ7Z$$O+O,O$7$$7$$$$$O:O??$$$$DMMMMM
                          MMMMMO$$$$Z7+,?O??7I?IZ$7~,8++$$$$7OZZZZOZOOZZ$$$$7O?,,O$$7$O?7IO?,I+O7$$ZDMMMMM
                          MMMMMO7$$$$Z++8I?I??IOIO,,8+?Z7$$$OZOOOZOZOZOZO$7$$$$+.,O$$7?????O7?O7$7$7NMMMMM
                          MMMMMO7$7$$7O+OII??IO?O,O++O$$$Z7$Z$OOOOOZOO$$ZO$77I7I?O,,OII+III$ZO7$$7$$NMMMMM
                          MMMMMO777$$$7O?I???O??+++O777$$77Z$ZZOZOZOZZZZOOI7777$O?++ZI7I7IIIZ$7$$$$$DMMMMM
                          MMMMMO7777$7ZI??IO???OOI$77I777$7$$$$7$$$7$$$7777$77$77$77I8?I?Z7??O$7$77$DMMMMM
                          MMMMM8OOOOOI??????O+OOOOZDDOOOOOZZO?+$O++O+++8+7MOOMMMO+?MMOII?I??II?IIOMMMMMMMM
                          MMMMMMO?I?I?II8II7?8M++==MMMMOZM78M8ZM8=OMO+NMO8D$OMMMI++MMMOI=??I$I??II7MMMMMMM
                          MMMMMOII?O?I7I?Z?OMMM=+==O$Z8O$OM8OMMOMOMOMN8OMMZZ8++++++MMMMMO$$I?OII?IIOMMMMMM
                          MMMMOIII7?I7??ONMMMM8?++=+==+O77Z$$O$7OO$7ZO777ZZ$I=+++++MMMMMMMMZ?OZ?IIIIOMMMMM
                          MMMM7I????IOMMMMMMMMO+I7+====7$7$777I77777$$$7$77Z+==+O++MMMMMMMMMMOO?I$I?OMMMMM
                          MMMMI?+?OZOMMMMMMMMMO=8O========O777I?II777$7$7O?=~~=O8=+MMMMMMMMMMOZOZ??IOMMMMM
                          MMMD++?8O?MMMMMMMMMMO==+===+=+++=8OMMMMMMMMMMMOO=~======OMMMMMMMMMMN??+??IOMMMMM
                          MMMD?+??I?MMMMMMMMMMMM$=+++++=====+==+==+++++++==+==+=7OMMMMMMMMMMMO?I?I+?MMMMMM
                          MMMMII?II?OMMMMMMMMMMMMMO=+++++~O==++==+$O=++?+=+++OOMMMMMMMMMMMMMMII7I??OMMMMMM
                          MMMMI??7?IIMMMMMMMMMMMMMMMMMOOOZ+=+=+==+==+=+?OOO+7OMMMMMMMMMMMMMMOI??O?+NMMMMMM
                          MMMMO??IOI?IONMMMMMMMMMDO8?I?O77777III777I77777IO+????+IOOMMMMMMMO7+?Z??OMMMMMMM
                          MMMMMO??IO77$7$$OMMOI?I?I?????IIOO77$77I777$OOOO??+???+II????O8O777$7O?OMMMMMMMM
                          MMMMMMOIO777$?,:,O???I?I?????I?I?OOOZZ$$$$ZOOO7+??I???+I????I?,,,O7$7$7MMMMMMMMM
                          MMMMMMMO777O,,,,,,????I??I?I????????+???????I??????????++????O,,,,,O7$7MMMMMMMMM
                          MMMMMMMO77O,,,OO,,O+?I??I??????I???????7????I????+??????????IO,OZ,,,Z$O7DMMMMMMM
                          MMMMMN7?$O,,,OZI,8II????I??IIOII?III??II?I??IIIII?+?I???O?????,,O=,,.O?IIOMMMMMM
                          MMMMO?IIOI,:,,,.OI?III???I?Z8?I???$II????I?78?+I???II??OOIII??O.,,,,OIII??ZMMMMM
                          MMMO???I??7O8OOI??III?III+?ZI??IZ777O??????77O7III?II???OI??IIIIIIIIII??IIIOMMMM
                          MMOII??I?II7IIIII7IIIIIIIIOIIIII7ZIO78??I?8O7O7O?I???I?I?OI??III?III?III?IIIOMMM
                          MNII?II?III7IO$$OO$II7IIIZ7IIIIZO7$7?Z?I?IO$OIO$8I???II?IIIO???IIIII?ZI??IIIIMMM
                          MO?IIII7IIIIO$$$$$$7$ZOO$III$IOZ77$7Z??III87$87O7OIII?II+I?$OOZ$$$$$7$7OI?IIIOMM
                          M??IIIIIII?O$Z$$$$Z$7$ZZZZZOZO$$$77$8I??II?$7$O7777OOOO$I$$$$$$$$$$7$$$$OIIIIOMM
                          8?II??I?II?77$$$Z$$$7$ZZZZZZZZ$$$$7$OIII?IO$$7$777$$$7$777$Z$$$$$$$$$$$$$IIIIIMM
                          8II???IIIIZ77$$$$Z$$$$$Z$$Z$O$O7?77$OI?I?IO7$$$$$77O$$Z$Z$$$$$?$$$$$$$ZZ$OI7IIOM
                          O??I??IIII8Z77$$ZZ$Z$$$$$Z$ZO$O$$$$$7IIII?IO777$7$$O$$$Z$$7$$Z$Z$$ZZ7$$$ZOII7IOM
                          MMN8OOO8MMMMN8OOO888NMMMMMN8OOOOOOOO8NMMMMMMMMMMMMMMOOOOOOOOOOOOOOOOOOOOOOOOOOOM

                                                        Contract written by:
                                              ____       _          _     _           _
                                             / __ \  ___| |__  _ __(_)___| |__   ___ | |
                                            / / _` |/ __| '_ \| '__| / __| '_ \ / _ \| |
                                           | | (_| | (__| | | | |  | \__ \ | | | (_) | |
                                            \ \__,_|\___|_| |_|_|  |_|___/_| |_|\___/|_|
                                             \____/

*/

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.7;

import "@openzeppelin/contracts/token/ERC1155/ERC1155.sol";
import "@openzeppelin/contracts/token/ERC1155/extensions/ERC1155Supply.sol";
import "@openzeppelin/contracts/token/ERC1155/extensions/ERC1155Burnable.sol";
import "@openzeppelin/contracts/utils/Strings.sol";
import "@openzeppelin/contracts/utils/cryptography/MerkleProof.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/security/Pausable.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";

contract MonsterBricks is ERC1155, ERC1155Burnable, ERC1155Supply, Ownable, Pausable, ReentrancyGuard {
  using Strings for uint256;
  uint256 private currentTokenId = 0;

  uint256 public maxTokenCount = 30;
  bytes32 public currentMerkleRoot;
  string public baseURI;

  mapping(address => uint256) public lastTokenClaimed;
  mapping(uint256 => string) public brickName;

  constructor(string memory _baseURI) ERC1155(_baseURI) {
    baseURI = _baseURI;
    pauseSale(); // Start paused
  }

  function uri(uint256 _tokenId) public view override returns (string memory) {
    require(_tokenId <= currentTokenId, "URI requested for invalid token");
    return
      bytes(baseURI).length > 0
        ? string(abi.encodePacked(baseURI, _tokenId.toString()))
        : baseURI;
  }

  function claimBrick(address _account, uint256 _quantity, bytes32[] calldata _proof) public whenNotPaused nonReentrant {
    require(verify(leaf(_account, _quantity), _proof), "Invalid merkle proof");
    require(lastTokenClaimed[_account] < currentTokenId, "Already claimed this token");

    lastTokenClaimed[_account] = currentTokenId;

    _mint(_account, currentTokenId, _quantity, "");
  }

  /* Merkle Tree Helper Functions */

  function leaf(address _account, uint256 _quantity) internal pure returns (bytes32) {
    return keccak256(abi.encodePacked(_account, _quantity));
  }

  function verify(bytes32 _leaf, bytes32[] memory _proof) internal view returns (bool) {
    return MerkleProof.verify(_proof, currentMerkleRoot, _leaf);
  }

  /* Owner Functions */

  function setMerkleRoot(bytes32 _merkleRoot) public onlyOwner {
    currentMerkleRoot = _merkleRoot;
  }

  function pauseSale() public onlyOwner {
    _pause();
  }

  function unpauseSale() public onlyOwner {
    _unpause();
  }

  function createNewRelease(string memory _name) public whenPaused onlyOwner {
    require(currentTokenId < maxTokenCount, "Cannot create more releases");

    currentTokenId += 1;
    brickName[currentTokenId] = _name;
  }

  function setBaseURI(string memory _baseURI) public onlyOwner {
    baseURI = _baseURI;
  }

  /* Overrides */

  function _mint(address account, uint256 id, uint256 amount, bytes memory data) internal override(ERC1155, ERC1155Supply) {
    super._mint(account, id, amount, data);
  }

  function _mintBatch(address account, uint256[] memory ids, uint256[] memory amounts, bytes memory data) internal override(ERC1155, ERC1155Supply) {
    super._mintBatch(account, ids, amounts, data);
  }

  function _burn(address account, uint256 id, uint256 amount) internal override(ERC1155, ERC1155Supply) {
    super._burn(account, id, amount);
  }

  function _burnBatch(address account, uint256[] memory ids, uint256[] memory amounts) internal override(ERC1155, ERC1155Supply) {
    super._burnBatch(account, ids, amounts);
  }
}

