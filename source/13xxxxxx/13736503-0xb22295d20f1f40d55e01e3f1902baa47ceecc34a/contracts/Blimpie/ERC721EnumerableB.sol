// SPDX-License-Identifier: BSD-3-Clause

pragma solidity ^0.8.0;

import "./ERC721B.sol";
import "./IBatch.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/IERC721Enumerable.sol";

/**
 * @dev This implements an optional extension of {ERC721} defined in the EIP that adds
 * enumerability of all the token ids in the contract as well as all token ids owned by each
 * account.
 */
abstract contract ERC721EnumerableB is ERC721B, IBatch, IERC721Enumerable {
    mapping(address => uint[]) internal _balances;

    function balanceOf(address owner) public view virtual override(ERC721B,IERC721) returns (uint) {
        require(owner != address(0), "ERC721: balance query for the zero address");
        return _balances[owner].length;
    }

    function isOwnerOf( address account, uint[] calldata tokenIds ) external view override returns( bool ){
        for(uint i; i < tokenIds.length; ++i ){
            if( _owners[ tokenIds[i] ] != account )
                return false;
        }

        return true;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(IERC165, ERC721B) returns (bool) {
        return interfaceId == type(IERC721Enumerable).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev See {IERC721Enumerable-tokenOfOwnerByIndex}.
     */
    function tokenOfOwnerByIndex(address owner, uint index) public view virtual override returns (uint tokenId) {
        require(index < ERC721B.balanceOf(owner), "ERC721Enumerable: owner index out of bounds");
        return _balances[owner][index];
    }

    /**
     * @dev See {IERC721Enumerable-totalSupply}.
     */
    function totalSupply() public view virtual override returns (uint) {
        return _owners.length - _offset;
    }

    /**
     * @dev See {IERC721Enumerable-tokenByIndex}.
     */
    function tokenByIndex(uint index) public view virtual override returns (uint) {
        require(index < totalSupply(), "ERC721Enumerable: global index out of bounds");
        return index + _offset;
    }

    function walletOfOwner( address account ) external view override returns( uint[] memory ){
        return _balances[ account ];
    }

    function transferBatch( address from, address to, uint[] calldata tokenIds, bytes calldata data ) external override{
        for(uint i; i < tokenIds.length; ++i ){
            safeTransferFrom( from, to, tokenIds[i], data );
        }
    }


    //internal
    function _beforeTokenTransfer(
        address from,
        address to,
        uint tokenId
    ) internal override virtual {
        address zero = address(0);
        if( from != zero ){
            //find this token and remove it
            uint length = _balances[from].length;
            for( uint i; i < length; ++i ){
                if( _balances[from][i] == tokenId ){
                    _balances[from][i] = _balances[from][length - 1];
                    _balances[from].pop();
                    break;
                }
            }
        }

        if( to != zero )
            _balances[to].push( tokenId );
    }
}

